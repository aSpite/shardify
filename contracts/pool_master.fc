#include "imports/stdlib.fc";
#include "constants.fc";

global slice storage::admin_address;
global int storage::public_key;

() load_data() impure {
    slice ds = get_data().begin_parse();
    storage::admin_address = ds~load_msg_addr();
    storage::public_key = ds~load_uint(256);
}

() save_data() impure {
    set_data(begin_cell()
        .store_slice(storage::admin_address)
        .store_uint(storage::public_key, 256)
        .end_cell()
    );
}

() recv_internal(int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) {
        return();
    }
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    if (flags & 1) {
        return ();
    }
    slice sender_address = cs~load_msg_addr();

    int op = in_msg_body~load_uint(32);
    int query_id = in_msg_body~load_uint(64);

    load_data();
    if(op == op::create_pool) {
        slice signature = in_msg_body~load_bits(512);
        cell payload = in_msg_body~load_ref();
        throw_unless(error::unauthorized, check_signature(cell_hash(payload), signature, storage::public_key));
        slice ps = payload.begin_parse();
        int price = ps~load_coins();
        throw_unless(error::insufficient_gas, msg_value >= (price + fee::pool_creation));
        int valid_until = ps~load_uint(32);
        throw_unless(error::old_message, valid_until > now());
        cell init_data = begin_cell()
            .store_coins(0)
            .store_slice(my_address())
            .store_slice(ps)
            .end_cell();
        cell init_code = in_msg_body~load_ref();
        in_msg_body.end_parse();
        raw_reserve(price, 4);
        cell state_init = begin_cell()
            .store_uint(0, 2)
            .store_dict(init_code)
            .store_dict(init_data)
            .store_uint(0, 1)
            .end_cell();
        slice minter_address = begin_cell()
            .store_uint(4, 3)
            .store_uint(const::workchain, 8)
            .store_uint(cell_hash(state_init), 256)
            .end_cell()
            .begin_parse();
        send_raw_message(begin_cell()
            .store_uint(flag::bounceable, 6)
            .store_slice(minter_address)
            .store_coins(100000000) ;; 0.1 TON
            .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1)
            .store_uint(1, 1)
            .store_ref(state_init)
            .store_uint(0, 1)
            .end_cell(), 1);

        send_raw_message(begin_cell()
            .store_uint(flag::non_bounceable, 6)
            .store_slice(sender_address)
            .store_coins(0)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_uint(op::excesses, 32)
            .store_uint(query_id, 64)
            .end_cell(), 130);
        return();
    }

    if(op == op::addr_change_admin) {
        throw_unless(error::unauthorized, equal_slices(sender_address, storage::admin_address));
        storage::admin_address = in_msg_body~load_msg_addr();
        save_data();
        return();
    }

    if(op == op::sign_change_admin) {
        slice signature = in_msg_body~load_bits(512);
        cell newAddr = in_msg_body~load_ref();
        throw_unless(error::unauthorized, check_signature(cell_hash(newAddr), signature, storage::public_key));
        slice addr = newAddr.begin_parse();
        int valid_until = addr~load_uint(32);
        throw_unless(error::old_message, valid_until > now());
        storage::admin_address = addr;
        save_data();
        return();
    }

    if(op == op::change_minter_pubkey) {
        throw_unless(error::unauthorized, equal_slices(sender_address, storage::admin_address));
        slice minter_address = in_msg_body~load_msg_addr();
        int public_key = in_msg_body~load_uint(256);
        send_raw_message(begin_cell()
            .store_uint(flag::bounceable, 6)
            .store_slice(minter_address)
            .store_coins(0)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_uint(op::change_pubkey, 32)
            .store_uint(query_id, 64)
            .store_uint(public_key, 256)
            .end_cell(), 64);
        return();
    }

    if(op == op::withdraw_nft_minter) {
        throw_unless(error::unauthorized, equal_slices(sender_address, storage::admin_address));
        slice minter_address = in_msg_body~load_msg_addr();
        cell body = begin_cell()
            .store_uint(op::withdraw_nft, 32)
            .store_uint(query_id, 64)
            .store_slice(in_msg_body)
            .end_cell();
        send_raw_message(begin_cell()
            .store_uint(flag::bounceable, 6)
            .store_slice(minter_address)
            .store_coins(0)
            .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_ref(body)
            .end_cell(), 64);
        return();
    }

    if(op == op::withdraw_ton_from_minter) {
        throw_unless(error::unauthorized, equal_slices(sender_address, storage::admin_address));
        send_raw_message(begin_cell()
            .store_uint(flag::bounceable, 6)
            .store_slice(in_msg_body)
            .store_coins(0)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_uint(op::withdraw_ton_minter, 32)
            .store_uint(query_id, 64)
            .end_cell(), 64);
        return();
    }

    if(op == op::change_minter_content) {
        throw_unless(error::unauthorized, equal_slices(sender_address, storage::admin_address));
        slice minter_address = in_msg_body~load_msg_addr();
        send_raw_message(begin_cell()
            .store_uint(flag::bounceable, 6)
            .store_slice(minter_address)
            .store_coins(0)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_uint(4, 32)
            .store_uint(query_id, 64)
            .store_slice(in_msg_body)
            .end_cell(), 64);
        return();
    }

    if(op == op::change_minter_admin) {
        throw_unless(error::unauthorized, equal_slices(sender_address, storage::admin_address));
        slice minter_address = in_msg_body~load_msg_addr();
        send_raw_message(begin_cell()
            .store_uint(flag::bounceable, 6)
            .store_slice(minter_address)
            .store_coins(0)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_uint(3, 32)
            .store_uint(query_id, 64)
            .store_slice(in_msg_body)
            .end_cell(), 64);
        return();
    }

    if(op == op::withdraw_ton_master) {
        throw_unless(error::unauthorized, equal_slices(sender_address, storage::admin_address));
        raw_reserve(10000000, 0); ;; 0.01 TON
        send_raw_message(begin_cell()
            .store_uint(flag::bounceable, 6)
            .store_slice(storage::admin_address)
            .store_coins(0)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .end_cell(), 128);
        return();
    }
}

(slice, int) get_admin_data() method_id {
    load_data();
    return(storage::admin_address, storage::public_key);
}